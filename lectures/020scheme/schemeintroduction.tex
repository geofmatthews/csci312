\documentclass{beamer}
\usetheme{Singapore}
\usepackage{changepage}

%\usepackage{pstricks,pst-node,pst-tree}
\usepackage{amssymb,latexsym}
\usepackage{tikz}
\usepackage{graphicx}
\usepackage{fancyvrb}
\usepackage{hyperref}
\usepackage{fancybox}
\usepackage[listings]{tcolorbox}

\definecolor{codegreen}{rgb}{0,0.6,0}
\definecolor{codegray}{rgb}{0.5,0.5,0.5}
\definecolor{codepurple}{rgb}{0.58,0,0.82}
\definecolor{backcolour}{rgb}{0.95,0.95,0.92}

\lstdefinestyle{mystyle}{
    language=Lisp,
    backgroundcolor=\color{backcolour},   
    commentstyle=\color{codegreen},
    keywordstyle=\color{magenta},
    numberstyle=\tiny\color{codegray},
    stringstyle=\color{codepurple},
    basicstyle=\ttfamily\footnotesize,
    breakatwhitespace=false,         
    breaklines=true,                 
    captionpos=b,                    
    keepspaces=true,                 
    numbers=left,                    
    numbersep=5pt,                  
    showspaces=false,                
    showstringspaces=false,
    showtabs=false,                  
    tabsize=2,
    escapechar=|,
    frame=single
}

\lstset{style=mystyle}


\newcommand{\bi}{\begin{itemize}}
\newcommand{\li}{\item}
\newcommand{\ei}{\end{itemize}}
\newcommand{\Show}[1]{
\begin{center}
\shadowbox{\begin{minipage}{0.8\textwidth}
          #1
          \end{minipage}}
\end{center}
}
\newcommand{\arrow}{\ensuremath{\rightarrow}}

\newcommand{\uparr}{\ensuremath{\uparrow}}


\newcommand{\fig}[2]{\centerline{\includegraphics[width=#1\textwidth]{#2}}}

\newcommand{\bfr}[1]{\begin{frame}[fragile]\frametitle{{ #1 }}}
\newcommand{\efr}{\end{frame}}

\newcommand{\cola}{\begin{columns}\begin{column}{0.5\textwidth}}
\newcommand{\colb}{\end{column}\begin{column}{0.5\textwidth}}
\newcommand{\colc}{\end{column}\end{columns}}


\title{Intro to Scheme}
\author{Geoffrey Matthews}

\begin{document}

\begin{frame}
\maketitle
\end{frame}


\bfr{Scheme}

Remember to work through the tutorials!

\bi
\li \url{https://racket-lang.org/}
\bi\li Our implementation \ei
\li \url{https://docs.racket-lang.org/index.html}
\bi\li Work through the {\em Quick} tutorial\ei
\li Also do one or more of the following:
\bi
\li \url{https://ds26gte.github.io/tyscheme/}
\bi\li Work through Chapters 1 to 6: Recursion
\li Note that we use Racket instead of mzscheme\ei
\li \url{https://www.scheme.com/tspl4/}
\bi\li Work through Chapters 1 and 2 
\li Note that we use Racket instead of chez scheme\ei
\li \url{https://htdp.org/}
\bi\li Very careful exposition.\ei
\ei\ei


\end{frame}

\bfr{Basic Scheme Syntax: Literal expressions}
\cola
\begin{lstlisting}
> 123
123
> 1.2e5
120000.0
> 1/3
1/3
> 'a
'a
> 'very-long-identifier
'very-long-identifier
> '(list of things 1 2 3)
'(list of things 1 2 3)
\end{lstlisting}
\colb
\begin{lstlisting}
> "a string"
"a string"
> ")) a bad string"
")) a bad string"
> '99
99
> 99
99
> #t
#t
> '#t
#t
\end{lstlisting}
\colc

\end{frame}


\bfr{Basic Scheme Syntax: Procedure calls}
\cola
\begin{lstlisting}
> (+ 3 4)
7
> (+ 3 (* 4 5))
23
> (* (+ 3 4) 5)
35
> ((if #f + *) 3 4)
12
> 
\end{lstlisting}
\colb
\begin{lstlisting}
3 + 4 * 5
\end{lstlisting}
\colc

\end{frame}

\bfr{Basic Scheme Syntax: Naming things}
\begin{lstlisting}
> (define x 2)
> (define y 3)
> x
2
> y
3
> (* x y)
6
\end{lstlisting}

\end{frame}
\bfr{Basic Scheme Syntax: Conditionals}
\begin{lstlisting}
> (define x 2)
> (define y 3)
> (if (< x y) x y)
2
> (cond ((< x y) 'yes)
        ((> x y) 'no)
        ((= x y) 'maybe)
        (else (error "Help!" (list x y))))
'yes
> 
\end{lstlisting}

\end{frame}

\bfr{Basic Scheme Syntax:  Case}
\begin{lstlisting}
> (case (* 2 3)
    ((2 3 5 7) 'prime)
    ((1 4 6 8 9) 'composite))
'composite
> 
\end{lstlisting}
\end{frame}

\bfr{Printing: three ways}
\begin{lstlisting}
(define astring 
  "foobuz
basssssssssssssss @#$@#@$")
(display astring)
(newline)
(print astring)
(newline) ;; Notice neither prints a newline by itself
(printf 
  "Formatted output: ~a and: ~a\n" 
  'aThing 1234)
\end{lstlisting}
\begin{lstlisting}
foobuz
basssssssssssssss @#$@#@$
"foobuz\nbasssssssssssssss @#$@#@$"
Formatted output: aThing and: 1234
\end{lstlisting}
\end{frame}

\bfr{Basic Scheme Syntax:  Sequencing}

\begin{lstlisting}
> (begin (display "4 plus 1 equals ")
         (display (+ 4 1))
         (newline))
4 plus 1 equals 5
> 
\end{lstlisting}
\end{frame}
\bfr{Two ways to define procedures}
\begin{lstlisting}
(define (square1 x) (* x x))
(define square2 (lambda (x) (* x x)))
\end{lstlisting}
\bi
\li  I like to use the second way.
\li The textbook uses the first way.
\li Simple unit tests also supported in the \lstinline{plai} language:
\ei
\begin{lstlisting}
> (define testnumber 23423)
> (test (square1 testnumber)
        (square2 testnumber))
(good (square1 testnumber) 548636929 548636929 "at line 31")
\end{lstlisting}
\end{frame}
\bfr{Rational numbers and many other builtin types}
\begin{lstlisting}
(test (/ 2 3)
      2/3)
\end{lstlisting}
\begin{lstlisting}
(good (/ 2 3) 2/3 2/3 "at line 35")
\end{lstlisting}
\end{frame}
\bfr{Two ways to quote things}
\begin{lstlisting}
(test 
 '(1 2 3 a b c)
 (quote (1 2 3 a b c)))
\end{lstlisting}
\begin{lstlisting}
(good '(1 2 3 a b c) '(1 2 3 a b c) '(1 2 3 a b c) "at line 39")
\end{lstlisting}
\bi\li We will use the first way.
\li Quoted expressions denote linked lists.\ei
\end{frame}
\bfr{Dot notation and List notation}

\bi
\li Make sure you can draw box and arrow diagrams for random collections
 of cons cells, lists, dotted lists, etc. 
 \li Check out \lstinline{boxarrow.rkt} from my github repository.
\ei
\begin{lstlisting}
(test '(1 . (2 . (3 . ())))
      '(1 2 3) )
(test (cons 3 (cons 2 (cons 1 '())))
      (list 3 2 1) )
(cons (cons 'a 'b) (list (list 'c (cons 'd 'e) 
                              (cons 'f (list 'g)))))
(list 1 (list 2 (list 3 (list 4 5))))
(cons 1 (cons 2 (cons 3 (cons 4 5))))
\end{lstlisting}
\begin{lstlisting}
(good '(1 2 3) '(1 2 3) '(1 2 3) "at line 49")
(good (cons 3 (cons 2 (cons 1 '()))) '(3 2 1) '(3 2 1) "at line 52")
'((a . b) (c (d . e) (f g)))
'(1 (2 (3 (4 5))))
'(1 2 3 4 . 5)
\end{lstlisting}
\end{frame}
\bfr{{\tt car}'s and {\tt cdr}'s can be smashed together}
\begin{lstlisting}
(define testlist '((a b) ((c d) e (f (g)))))
(test (cdr (car (cdr testlist)))
      (cdadr testlist))
\end{lstlisting}
\end{frame}
\bfr{{\tt let} introduces local variables}
\begin{lstlisting}
(let ((x 3)
      (y 4))
  (test (* x y)
        (* 3 4)))

;; This doesn't work:

;;(let ((x 3)
;;      (y (* 5 x)))
;;   (* x y))
\end{lstlisting}
\end{frame}
\bfr{Use nested let's if you want new variables to depend on each other}
\begin{lstlisting}
(let ((x 3))
  (let ((y (* 5 x)))
    (* x y)))
\end{lstlisting}
\end{frame}
\bfr{Shadowed variables}
\begin{lstlisting}
(let ((b 99999))
  (let ((a 100)
        (b 1000)
        (c 10000))
    (+ a b c)))

(let ((b 99999))
  (+ (let ((a 100)
           (b 1000)
           (c 10000))
       (+ a b c))
     b))
\end{lstlisting}

\end{frame}
\bfr{Functions are first class values}
\begin{lstlisting}
(let ((f (lambda (g x) (+ (* 2 x) (g x)))))
  (f (lambda (y) (* 3 y)) 5))
\end{lstlisting}  
\pause
\begin{lstlisting}
(test
 (let ((+ *)) (+ 3 3))
 (* 3 3))
\end{lstlisting}
\end{frame}
\bfr{{\tt lambda} and {\tt let} are similar}
\begin{lstlisting}
(test
 (let ((x 3) (y 4)) (list 5 x y))
 ((lambda (x y) (list 5 x y)) 3 4))
\end{lstlisting}
\end{frame}
\bfr{Free variables can be captured by lambda}
\begin{lstlisting}
(let ((x 'sam))
  (let ((f (lambda (y z) (list x y z))))
    (f 'i 'am)))

\end{lstlisting}
\bi
\li
Free variables remain captured even when shadowed:
\ei
\begin{lstlisting}
(let ((x 'sam))
  (let ((f (lambda (y z) (list x y z))))
    (let ((x 'mary))
      (f 'i 'am))))
\end{lstlisting}
\end{frame}
\bfr{{\tt lambda} creates a {\bf closure}}
\begin{lstlisting}
(define f1
  (let ((x 'sam))
    (lambda (y z) (list x y z))))

(f1 'i 'am)
(define x 'nobody)
(f1 'i 'am)
\end{lstlisting}
\begin{lstlisting}
'(sam i am)
'(sam i am)
\end{lstlisting}
\end{frame}
\bfr{We can make objects with local state}
\begin{lstlisting}
(define counter
  (let ((n 0))
    (lambda ()
      (set! n (+ n 1))
      n)))

(counter)
(counter)
(counter)
(counter)
\end{lstlisting}
\begin{lstlisting}
1
2
3
4
\end{lstlisting}
\end{frame}
\bfr{Functions can return functions}
\begin{lstlisting}
(define adder
  (lambda (x)
    (lambda (y) (+ x y))))

(define f (adder 10))
(define g (adder 100))
(list (f 3) (f 5) (g 3) (g 5))
\end{lstlisting}


\begin{lstlisting}
`(13 15 103 105)
\end{lstlisting}

\bi
\li This particular example is called {\bf Currying} a function--taking a many-argument function 
and turning it into a one argument function.
\ei


\end{frame}
\bfr{We can use closures to store local data}
\begin{lstlisting}
(define triple
  (lambda (a b c)
    (lambda (op)
      (cond ((eqv? op 'first) a)
            ((eqv? op 'second) b)
            ((eqv? op 'third) c)))))

(define a (triple 5 9 20))
(define b (triple 'hello 'goodbye 'whatever))
(list (a 'first) (b 'second) (a 'third) (b 'first))
\end{lstlisting}
\begin{lstlisting}
'(5 goodbye 20 hello)
\end{lstlisting}
\end{frame}
\bfr{We can use closures to create objects like stacks}
\begin{lstlisting}
(define stack
  (lambda ()
    (let ((the-stack '()))
      (lambda (op . args)
        (cond ((eq? op 'push)
               (set! the-stack (cons (car args)
                                     the-stack)))
              ((eq? op 'pop)
               (let ((top (car the-stack)))
                 (set! the-stack (cdr the-stack))
                 top))
              (else
               (error "Unknown stack operator:  ~a"
                       op)))))))
(define s (stack))
(s 'push 99)
(s 'push 101)
(list (s 'pop) (s 'pop))
\end{lstlisting}
\begin{lstlisting}
'(101 99)
\end{lstlisting}
\end{frame}




\bfr{List recursion}
\begin{lstlisting}
(require racket/trace)
(define list-length
  (lambda (lst)
    (if (null? lst) 0
        (+ 1 (list-length (cdr lst))))))
(trace list-length)
(list-length '(a b c d))
\end{lstlisting}
\begin{lstlisting}>
(list-length '(a b c d))
> (list-length '(b c d))
> >(list-length '(c d))
> > (list-length '(d))
> > >(list-length '())
< < <0
< < 1
< <2
< 3
<4
4
\end{lstlisting}

\end{frame}
\bfr{Tail recursion}
\begin{lstlisting}
(define list-length-tail
  (lambda (lst result)
    (if (null? lst) result
        (list-length-tail (cdr lst) (+ 1 result)))))
(trace list-length-tail)
(list-length-tail '(a b c d) 0)
\end{lstlisting}
\begin{lstlisting}
>(list-length-tail '(a b c d) 0)
>(list-length-tail '(b c d) 1)
>(list-length-tail '(c d) 2)
>(list-length-tail '(d) 3)
>(list-length-tail '() 4)
<4
4
\end{lstlisting}
\bi
\li Note an extra parameter is required.
\li This can be eliminated with a front-end function
\ei
\end{frame}
\bfr{Nontail copy  vs. tail copy}
\begin{lstlisting}
(define list-copy
  (lambda (lst)
    (if (null? lst) '()
        (cons (car lst)
              (list-copy (cdr lst))))))
                         result)))))
\end{lstlisting}
\begin{lstlisting}
>(list-copy '(a b c d))
> (list-copy '(b c d))
> >(list-copy '(c d))
> > (list-copy '(d))
> > >(list-copy '())
< < <'()
< < '(d)
< <'(c d)
< '(b c d)
<'(a b c d)
'(a b c d)\end{lstlisting}
\end{frame}
\bfr{Nontail copy  vs. tail copy}
\begin{lstlisting}
(define list-copy-tail
  (lambda (lst result)
    (if (null? lst) (reverse result)
        (list-copy-tail (cdr lst) (cons (car lst)
                                        result)))))
\end{lstlisting}
\begin{lstlisting}
>(list-copy-tail '(a b c d) '())
>(list-copy-tail '(b c d) '(a))
>(list-copy-tail '(c d) '(b a))
>(list-copy-tail '(d) '(c b a))
>(list-copy-tail '() '(d c b a))
<'(a b c d)
'(a b c d)
\end{lstlisting}
\end{frame}




\bfr{Tree recursion}
\begin{lstlisting}
(define tree-copy (lambda (tree)
    (if (not (pair? tree)) tree
        (cons (tree-copy (car tree)) (tree-copy (cdr tree))))))
\end{lstlisting}
\cola
\begin{lstlisting}[basicstyle=\tiny]
>(tree-copy '((a) (b)))
> (tree-copy '(a))
> >(tree-copy 'a)
< <'a
> >(tree-copy '())
< <'()
< '(a)
> (tree-copy '((b)))
> >(tree-copy '(b))
> > (tree-copy 'b)
< < 'b
> > (tree-copy '())
< < '()
< <'(b)
> >(tree-copy '())
< <'()
< '((b))
<'((a) (b))
\end{lstlisting}
\colb
\bi
\li Tail recursion not possible.
\li Not a linear process
\ei
\colc

\end{frame}
\bfr{Named {\tt let}}
\bi
\li Named function:
\begin{lstlisting}
(define fib
  (lambda (n)
    (if (< n 2) 1
        (+ (fib (- n 2))
           (fib (- n 1))))))
(fib 10)
\end{lstlisting}
\li Named {\tt let}
\begin{lstlisting}
(let fib ((n 10))
  (if (< n 2) 1
      (+ (fib (- n 2))
         (fib (- n 1)))))
\end{lstlisting}
\ei
\end{frame}



\bfr{Local recursive functions}

\begin{lstlisting}
(define mod2
  (lambda (n)
    (letrec ((even?
              (lambda (n)
                (if (zero? n)
                    #t
                    (odd? (- n 1)))))
             (odd?
              (lambda (n)
                (if (zero? n)
                    #f
                    (even? (- n 1)))))))
  (cond ((even? n) 0)
        ((odd? n) 1)
        (else 0))))
\end{lstlisting}
\begin{lstlisting}
> (mod2 2341)
1
\end{lstlisting}
\end{frame}
\bfr{Local recursive functions}

\begin{lstlisting}
(define (mod2 n)
  [local
    [(define (even? n)
       (if (zero? n)
           #t
           (odd? (- n 1))))
     (define (odd? n)
       (if (zero? n)
           #f
           (even? (- n 1))))]
    (cond ((even? n) 0)
          ((odd? n) 1)
          (else 0))])
\end{lstlisting}
\begin{lstlisting}
> (mod2 2341)
1
> 
\end{lstlisting}


\end{frame}


\bfr{{\tt define-type} from PLAI}
\begin{lstlisting}
#lang plai

(define-type foo
  (bar (x number?))
  (mug (x string?)))

(define (double x)
  (type-case foo x
    (bar (x) (* 2 x))
    (mug (x) (string-append x x))))

(let ((myfoo (bar 99))
      (nofoo (mug "hello")))
  (list (double myfoo)
        (double nofoo)))
\end{lstlisting}
\end{frame}
\bfr{{\tt define-type} from PLAI}
\begin{lstlisting}
#lang plai
(define-type position
  (2d (x number?) (y number?))
  (3d (x number?) (y number?) (z number?)))

(define-type shape
  (circle (center 2d?) 
          (radius number?))
  (square (lower-left 2d?) 
          (width number?) 
          (height number?)))
\end{lstlisting}
\begin{lstlisting}
(define (area s)
  (type-case shape s
    (circle (center radius)
            (* pi radius radius))
    (square (lower-left width height)
            (* width height))))
\end{lstlisting}
\end{frame}

\bfr{Center of a shape}
\begin{lstlisting}
(define (center s)
  (type-case shape s
    (circle (center radius) center)
    (square (lower-left width height)
            (type-case position lower-left
              (2d (x y)
                  (2d (+ x (/ width 2)) 
                      (+ y (/ height 2))))
              (else (error "bad square" s))))))
\end{lstlisting}
\end{frame}
\bfr{Returning multiple values}
\begin{lstlisting}
(define foo
  (lambda (x) (values x (* x x))))

(define bar
  (lambda (x) (values x (* x x x))))

(let-values (((a b) (foo 3))
             ((c d) (bar 3)))
  (list a b d))
\end{lstlisting}
\end{frame}


\bfr{Unit testing}
\cola
\begin{lstlisting}[title=arithmetic.rkt]
#lang plai

(define slow+
  (lambda (a b)
    (if (zero? a) b
        (slow+ (sub1 a)
               (add1 b)))))

(define slow*
  (lambda (a b)
    (if (zero? a) 0
        (slow+ b 
          (slow* 
            (sub1 a) b)))))

\end{lstlisting}
\colb
\begin{lstlisting}[title=arithmetic-test.rkt]
#lang plai

(require rackunit
         "arithmetic.rkt")

;; These provided by plai:
(test (slow+ 4 5) 9)
(test (slow* 4 5) 21)

;; These by rackunit:
(check-equal?
  (slow+ 4 5) 9)
(check-equal?
  (slow* 4 5) 21)

\end{lstlisting}
\colc
\end{frame}
\bfr{Homework 2:  Programming Scheme}
\bi
\li Programming puzzles in Scheme.
\li Must use {\bf natural recursion} and not builtin Scheme functionality.
\li Review {\tt naturalrecursion.rkt}
\ei
\end{frame}
\end{document}
